#!/icg/bin/perl
use strict;
use warnings;
use DNS::Oterica::Hub;
use File::Find::Rule;
use YAML ();

my $hub = DNS::Oterica::Hub->new;
$hub->add_location($_) for (
  { name => 'office',      code => ''   },
  { name => 'fastnet',     code => ''   },
  { name => 'quonix',      code => ''   },
  { name => 'fastnet-dmz', code => 'fn' },
  { name => 'quonix-dmz',  code => 'qx' },
);

my @files = File::Find::Rule->file->in('etc');

for my $file (@files) {
  for my $data (YAML::LoadFile($file)) {
    my $location = $hub->location($data->{location});

    my $interfaces;
    if (ref $data->{ip}) {
      $interfaces = [
        map {; [ $data->{ip}{$_} => $hub->location($_) ] } keys %{ $data->{ip}}
      ];
    } else {
      $interfaces = [ [ $data->{ip} => $hub->location('world') ] ];
    }

    my $node = $hub->node(
      $data->{domain},
      $data->{hostname},
      {
        interfaces => $interfaces,
        location   => $data->{location},
      },
    );

    for my $name (@{ $data->{roles} }) {
      my $role = $hub->node_role($name);

      $node->add_to_role($role);
    }
  }
}

my @nodes = sort { ($a->location cmp $b->location) || ($a->name cmp $b->name) }
            $hub->nodes;

print $_->as_data_lines for @nodes;
print "\n";
print $_->as_data_lines, "\n" for $hub->node_roles;
