#!/icg/bin/perl
use strict;
use warnings;
use DNS::Oterica::Hub;
use File::Find::Rule;
use YAML ();

my $hub = DNS::Oterica::Hub->new;

my @files = File::Find::Rule->file->in('etc');

for my $file (@files) {
  my ($data) = YAML::LoadFile($file);

  my $node = $hub->node(
    $data->{domain},
    $data->{hostname},
    {
      ip       => $data->{ip}{world},
      location => $data->{location},
    },
  );

  for my $name (@{ $data->{roles} }) {
    my $role = $hub->node_role($name);

    $node->add_to_role($role);
  }
}

my @nodes = sort { ($a->location cmp $b->location) || ($a->name cmp $b->name) }
            $hub->nodes;

print $_->as_data_lines for @nodes;
print "\n";
print $_->as_data_lines, "\n" for $hub->node_roles;
